#!/usr/bin/perl -w

use strict;
use DBI;
use Getopt::Long;
use List::MoreUtils qw/ uniq /;

my $dbhost="127.0.0.1";
my $database="mythconverg";
my $user="mythtv";
my $pass="mythtv";
my $sourceid = 1;
my $hidden = 0;
my $conf="channels.conf";

GetOptions('dbhost=s'=>\$dbhost,
           'database=s'=>\$database,
           'user=s'=>\$user,
           'pass=s'=>\$pass,
           'sourceid=i'=>\$sourceid,
           'hidden!'=>\$hidden,
	   'conf=s'=>\$conf
           ) || die "Usage:";


my %channels;

# parse provided config fix
print "Parsing channels file: $conf\n\n";
open CONFIG, "<$conf" or die "Failed to open channels.conf file $conf";
my $line;

while (defined($line = <CONFIG>))
{
    chomp($line);
    my @parts = split(':',$line);
    my $name = $parts[0];
    my $freq = $parts[1];
    my $info = $parts[2];
    my $serviceid = $parts[9];

    # parse info block
    my $polarity = substr $info, 0, 1;
    my $fec = substr $info, 2, 2;
    my $modulation = substr $info, 5, 1;
    my $rolloff = substr $info, 7, 2;

    my $info2 = join "", $polarity, "C", $fec, "M", $modulation, "O", $rolloff;

    # put into hashref
    $channels{$serviceid} = {
	name => $name,
	frequency => $freq,
	info => $info2,
	polarity => $polarity,
	fec => $fec,
	modulation => $modulation,
	rolloff => $rolloff
    };
}
close CONFIG;


my $dbh = DBI->connect("dbi:mysql:database=$database:host=$dbhost","$user","$pass") || die "Cannot connect to database ($!)\n";

my $sql = sprintf("SELECT callsign, frequency, polarity, modulation, symbolrate, serviceid, fec, rolloff
                   FROM channel JOIN dtv_multiplex USING (mplexid)
                   WHERE channel.sourceid = ? %s
                   ORDER BY channum",
                  ($hidden ? "" : "AND channel.visible = 1")
                  );
my $sth = $dbh->prepare($sql);
$sth->execute($sourceid);

my @moved_channels;
my @missing_channels;
my @changed_sid;
my @tune_needed;

while (my @row=$sth->fetchrow_array) {
    my $callsign = $row[0];
    $callsign =~ s/:/ /g;
    my $frequency = int($row[1] / 1000);
    my $polarity = uc $row[2];
    my $modulation = ($row[3] eq "qpsk") ? "2" : "5";
    my $type = ($modulation eq "5") ? "S1" : "";
    my $symbolrate = int($row[4] / 1000);
    my $serviceid = $row[5];
    my $fec = $row[6];
    $fec =~ s!/!!g;
    my $rolloff = $row[7] * 100;

    my $info = join "", $polarity, "C", $fec, "M", $modulation, "O", $rolloff, $type;

    my $match = $channels{$serviceid};
    if (defined($match) && ($match->{'name'} eq ($callsign . ";BSkyB")))
    {
	# check if frequency and info the same
	if ((abs($match->{'frequency'} - $frequency) < 5) && 
	    ($match->{'polarity'} eq $polarity) && 
	    ($match->{'modulation'} eq $modulation) && 
	    ($match->{'fec'} eq $fec))
	{ }
	else
	{
	    print "$callsign - found match $match->{'name'}\n";
	    print "  Details differ. Scan: $match->{'frequency'}:$match->{'info'} MythTV: $frequency:$info\n";
	    push @moved_channels, $serviceid;
	    push @tune_needed, $match->{'frequency'} . ":" . $match->{'info'};
	}
    }
    else
    {
	push @missing_channels, $serviceid;

	# look for name matches
	my @name_matches = grep { $channels{$_}->{'name'} eq ($callsign . ";BSkyB") } keys %channels;

	if (scalar @name_matches < 1) {
	    print "$callsign ($serviceid): no match on ServiceID or name\n";
	} elsif (scalar @name_matches eq 1) {
	    print "$callsign ($serviceid): ServiceID changed to @name_matches\n";
	    push @changed_sid, $serviceid;
	    
	    my $name_match = $channels{$name_matches[0]};
	    push @tune_needed, $name_match->{'frequency'} . ":" . $name_match->{'info'};
	} else {
	    print "$callsign ($serviceid): ServiceID changed.  Multiple name matches: " . join(',', @name_matches) . "\n";
	}
    }
}

$dbh->disconnect;

if (scalar @missing_channels > 0) {
    print "\nDelete missing channels:\n";
    print "DELETE FROM channel WHERE serviceid IN (" . join(',', @missing_channels) . ");\n";
}

if (scalar @moved_channels > 0) {
    print "\nDelete moved channels:\n";
    print "DELETE FROM channel WHERE serviceid IN (" . join(',', @moved_channels) . ");\n";
}

if (scalar @tune_needed > 0) {
    print "\nTune these frequencies:\n";
    print join("\n", uniq @tune_needed) . "\n";
}

